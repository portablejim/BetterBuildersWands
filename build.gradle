buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.8'
    id 'org.ajoberstar.grgit' version '1.3.2'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: "org.ajoberstar.grgit"

import org.ajorberstar.grgit.*

ext.git = grgit.open(file('.'))

ext {
    configFile = file "build.properties"

    revision = git.head().abbreviatedId
    depth = git.log().size()
}

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    //noinspection GrUnresolvedAccess
    ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group= "portablejim.bbw.betterbuilderswands" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BetterBuildersWands"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = "snapshot_20161208"
    runDir = "run"
}

version = "${config.mod_version}-${config.minecraft_version}r${ext.depth}+${ext.revision}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'

        // replace version and mcversion
        expand 'version':project.config.mod_version, 'mcversion':project.config.minecraft_version, 'gitrev': revision,  'revnumber': depth
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

//noinspection GrUnresolvedAccess
task release(type: Copy) {
    dependsOn "build"

    from project.tasks.jar.archivePath
    into "release/"
}

build.dependsOn deobfJar

artifacts {
    archives deobfJar
}

configurations {
    deployerJars
}

uploadArchives {
    dependsOn 'reobf'

    repositories {
        if(project.hasProperty("pushMaven")) {
            logger.info("Pushing to maven repo")

            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: "file://localhost/home/public_html/maven.jamesmckay.id.au/htdocs/")

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    packaging = 'jar'
                    description 'VeinMiner Mod'
                }
            }
        }
    }
}

curseforge {
    if (project.hasProperty('curseForgeApiKey')) apiKey = project.curseForgeApiKey
    else apiKey = 0
    //debug = true
    project {
        id = '238403'
        changelog = new File('Changelog.md').getText('utf-8').split('\n-{3,8}|\n\n')[2]
        changelogType = 'text'
        releaseType = 'alpha' // Just a default
        mainArtifact (jar) {
            displayName = "VeinMiner $config.mod_version [MC$config.compatible_versions; rev $depth]"
        }

        addArtifact deobfJar

        for (String newVersion : config.compatible_versions.split(",")) {
            addGameVersion newVersion
        }
    }
}

task curseAlpha(dependsOn: tasks.curseforge) {}
task curseBeta(dependsOn: tasks.curseforge) {}
task curseRelease(dependsOn: tasks.curseforge) {}

gradle.taskGraph.whenReady { taskGraph ->
    curseforge.curseProjects.each { it.mainArtifact.displayName = "Better Builder's Wands $config.mod_version [MC$config.compatible_versions; rev $ext.depth]" }
    if (taskGraph.hasTask(curseAlpha)) {
        curseforge.curseProjects.each { it.additionalArtifacts.each { it.releaseType = 'alpha' } }
        curseforge.curseProjects.each { it.releaseType = 'alpha' }
        curseforge.curseProjects.each { it.mainArtifact.releaseType = 'alpha' }
    } else if (taskGraph.hasTask(curseBeta)) {
        curseforge.curseProjects.each { it.releaseType = 'beta' }
        curseforge.curseProjects.each { it.mainArtifact.releaseType = 'beta' }
        curseforge.curseProjects.each { it.additionalArtifacts.each { it.releaseType = 'beta' } }
    } else if (taskGraph.hasTask(curseRelease)) {
        curseforge.curseProjects.each { it.releaseType = 'release' }
        curseforge.curseProjects.each { it.mainArtifact.releaseType = 'release' }
        curseforge.curseProjects.each { it.additionalArtifacts.each { it.releaseType = 'release' } }
    }
}


